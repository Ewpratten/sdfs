# Hello Assembly

At this point in the book, I have talked about the following:

- Memory
- How the CPU executes machine code
- How assembly translates to machine code
- What registers are and how to use them
- System calls

Now that we are going to write a proper hello world program, we need to use all of these together.

Firstly, a small tweak needs to made to how we have been structuring assembly code so far. Up until this point, all assembly code in the book has been written for some imaginary computer that can only load one program into memory, stores that program at address `0x00` and executes the first byte.

Now, our code needs to be written so that it can be run just like any other program on your computer. This really only means one thing, Instead of starting our assembly code like this:

```x86asm
main:
```

It must be started like this:

```x86asm
.global _start
.text
    _start:
```

You can think of those few extra lines as a bit of metadata, telling the operating system *where* to find the first instruction of your program.

Alright, enough preface, time to write some code! I'll start by outlining exactly what needs to happen in order to print `Hello, World!` to the screen.

 1. The program starts
 2. We store the letters `Hello, World!` somewhere in memory, in order. (this is called a string)
 3. We configure the registers `%rax` and `%rdi` to specify we want to call the `write` system call
 4. We save the memory address of the first character of our string to the register `%rsi` so the syscall knows what we want to print
 5. We save the length of the string to the register `%rdx` so the syscall knows how many characters to print
 6. We call the `write` syscall
 7. We exit the program

Here is what the whole program looks like:

```x86asm
# The new program header we need
.global _start
.text

    # Where the program starts
    _start:
        mov $1, %rax       # Specify we want system call 1 (the write call)
        mov $1, %rdi       # Specify we want to write to the terminal
        mov $message, %rsi # Tell the system call where our string is in memory
        mov $14, %rdx      # Tell the system call how big the string is
        syscall            # Call the system call

        # Our exit code
        mov $60, %rax      # Specify we want system call 60 (the exit call)
        xor %rdi, %rdi     # Set rdi to 0 (this means the program was successful)
        syscall            # Call the system call

    # The string containing our message
    message:
        .ascii "Hello, World!\n" # The \n at the end of this makes it print on its own line

```

Reminder: This is an actual valid program! Save it to a file called `hello.s`, then run the following in your terminal to execute it:

```sh
gcc -nostdlib -no-pie hello.s && ./a.out
```